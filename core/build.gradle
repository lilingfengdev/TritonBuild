plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

group 'com.rexcantor64.triton'

test {
    useJUnitPlatform()
}

dependencies {
    compileOnly project(':core:triton-core-loader')
    compileOnly project(":api")

    // Adventure / MiniMessage
    compileOnly 'net.kyori:adventure-text-serializer-gson:4.17.0'
    compileOnly 'net.kyori:adventure-text-serializer-legacy:4.17.0'
    compileOnly 'net.kyori:adventure-text-serializer-plain:4.17.0'
    compileOnly('net.kyori:adventure-text-minimessage:4.17.0') {
        exclude group: 'net.kyori', module: 'adventure-api'
        exclude group: 'net.kyori', module: 'adventure-key'
        exclude group: 'net.kyori', module: 'adventure-text-serialize-gson'
        exclude group: 'net.kyori', module: 'adventure-text-serialize-legacy'
        exclude group: 'net.kyori', module: 'adventure-text-serialize-plain'
    }

    // Dependencies we can depend on, since they are on all platforms
    compileOnly 'com.google.guava:guava:31.1-jre'

    compileOnly 'org.apache.logging.log4j:log4j-core:2.17.1'
    compileOnly 'org.yaml:snakeyaml:2.0'

    implementation 'org.slf4j:slf4j-nop:1.7.36'
    implementation 'com.zaxxer:HikariCP:4.0.3'
    implementation('com.tananaev:json-patch:1.1') {
        exclude group: 'com.google.code.gson', module: 'gson'
    }
    compileOnly 'com.google.code.gson:gson:2.9.0'

    implementation 'net.byteflux:libby-core:1.3.0'

    // Test dependencies
    testImplementation project(":api")

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'

    testImplementation 'net.kyori:adventure-api:4.17.0'
    testImplementation 'net.kyori:adventure-text-serializer-gson:4.17.0'
    testImplementation 'net.kyori:adventure-text-serializer-legacy:4.17.0'
    testImplementation 'net.kyori:adventure-text-serializer-plain:4.17.0'
}

shadowJar {
    archiveFileName = 'triton-core.jarinjar'

    relocate 'org.bstats', 'com.rexcantor64.triton.metrics'
    //relocate 'org.slf4j', 'com.rexcantor64.shaded.slf4j'
    relocate 'com.tananaev.jsonpatch', 'com.rexcantor64.shaded.jsonpatch'
    relocate 'com.zaxxer.hikari', 'com.rexcantor64.shaded.hikari'

    relocate 'net.byteflux.libby', 'com.rexcantor64.triton.lib.libby'
}
